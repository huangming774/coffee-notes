allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            project.android {
                if (namespace == null) {
                    namespace project.group.toString()
                }
                compileSdkVersion 34
            }
        }
        if (project.plugins.hasPlugin("com.android.application") ||
                project.plugins.hasPlugin("com.android.library")) {
            project.dependencies {
                implementation "androidx.core:core-ktx:1.13.1"
            }
        }
        project.tasks.withType(JavaCompile).configureEach { task ->
            if (task.options?.compilerArgs != null) {
                task.options.compilerArgs.removeAll { it == '-Werror' }
                if (!task.options.compilerArgs.contains('-Xlint:-options')) {
                    task.options.compilerArgs.add('-Xlint:-options')
                }
            }
        }
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
